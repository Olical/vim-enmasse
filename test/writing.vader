Before (define test data and setup EnMasse):
  let original = ["Hello, World!", "This is EnMasse.", "EnMasse is useful."]
  let expected = ["Hello, World!", "This is EnMasse, a Vim plugin.", "EnMasse is handy."]
  let filePath = "test/writeable.txt"
  call writefile(original, filePath)
  execute "silent grep! EnMasse " . filePath
  EnMasse

After (remove the test file):
  call delete(filePath)

Execute (editing and writing in an EnMasse buffer changes the file):
  %s/is EnMasse/is EnMasse, a Vim plugin/
  %s/useful/handy/
  write
  let actual = readfile(filePath)
  AssertEqual expected, actual

Execute (will not let you write if a line is deleted):
  normal dd
  redir => messages
  write
  redir END
  let actual = readfile(filePath)
  let latestMessage = get(split(messages, "\n"), -1, "")
  AssertEqual original, actual
  AssertEqual "EnMasse: Mismatch between buffer lines and quickfix list. Refusing to write.", latestMessage

Execute (will not let you write if a line is added):
  normal o
  redir => messages
  write
  redir END
  let actual = readfile(filePath)
  let latestMessage = get(split(messages, "\n"), -1, "")
  AssertEqual original, actual
  AssertEqual "EnMasse: Mismatch between buffer lines and quickfix list. Refusing to write.", latestMessage

Execute (doesn't write if no lines have changed):
  let before = getftime(filePath)
  write
  let after = getftime(filePath)
  AssertEqual before, after